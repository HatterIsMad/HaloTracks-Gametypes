include "strings/common_strings.txt"





;	# String Tables #

	string_table english
		engine_category_slayer			"Slayer"
		title							"Race"
		description						"Gentlemen, start your engines."
		objective						"Hit checkpoints to complete laps.\r\n%n laps to win."
		optionNameNumberOfLaps			"Number of Laps"
		optionDescriptionNumberOfLaps	"Determines the number of laps players must attempt to complete to win the race."
		optionNameVehicle				"Vehicle"
		optionDescriptionVehicle		"Determines what vehicle players will use in this race."
		optionVehicleMongoose			"Mongoose"
		optionVehicleWarthog			"Warthog"
		optionNameCVDelete				"Competitive"
		optionDescriptionCVDelete		"If enabled, objects with the label cv_delete will be deleted."
		optionNameStartGate				"Start Gate"
		optionDescriptionStartGate		"If enabled, objects with the label race_gate will disappear at the selected time."
		optionStartGate1				"20 +/- 5"
		optionStartGate2				"30 +/- 10"
		optionStartGate3				"40 +/- 20"
		optionStartGate4				"55 +/- 5"
		lapsPlus1						"Time Limit + 1 Lap"
		lapsPlus2						"Time Limit + 2 Laps"
		empty							""
		widgetText						"Lap Time: %n.%n"
		widgetText2						"Lap Time: %n.0%n"
		finalLapMessage					"Final Lap."
		holeshotMessage					"%p got the holeshot"
		overallBestLapMessage			"Overall Best lap time! %n.%n"
		overallBestLapPlayerMessage		"Lap %n %p got the fastest lap"
		personalBestLapMessage			"Personal Best lap time! %n.%n"
		lapCompleteMessage				"Lap Complete: %n.%n"
		debug							"%n"
	end

;	# String Tables #



;	# Engine Config #

	engine_data
		name title
		description description
		icon 7
		category slayer
	end

;	# Engine Config #





;	# Object Labels #

	map_object race_checkpoint
		label "race_checkpoint"
		min 1
	end

	map_object race_delete
		label "race_delete"
	end

	map_object race_holeshot
		label "race_holeshot"
	end

	map_object race_hide
		label "race_hide"
	end

	map_object race_gate
		label "race_gate"
	end

	map_object cv_delete
		label "cv_delete"
	end

	map_object vehicle
		label "vehicle"
	end

	map_object Target
		label "Target"
	end


;	# Object Labels #





;	# Game Options #

	loadout_palette empty_palette
	end

	game_options

		lock override	teams_enabled					0
		override		respawn_time					3
		override		suicide_respawn_penalty			3
		lock override	score_to_win_round				500
		override		round_count						1
		override		round_time_limit				10
		override		early_victory_win_count			0
		override		indestructible_vehicles			1
		override		grenades_on_map					0
		override		vehicle_set						no_vehicles

		override		loadout_selection_time			0
		override		loadout_palette spartan_tier1	empty_palette
		override		loadout_palette spartan_tier2	empty_palette
		override		loadout_palette spartan_tier3	empty_palette
		override		loadout_palette elite_tier1		empty_palette
		override		loadout_palette elite_tier2		empty_palette
		override		loadout_palette elite_tier3		empty_palette

		override		base_player_traits
						damage_multiplier				0
						initial_secondary_weapon		sticky_grenade_launcher
						initial_primary_weapon			magnum
						melee_damage_multiplier			0
						initial_grenade_count			none
						shield_hud						false
						initial_equipment				sprint_equipment
						assassination_immunity			true
						headshot_immunity				true
						damage_resistance				true
						vehicle_usage					not_passenger
						infinite_ammo					on
		end

		option	numberOfLaps	optionNameNumberOfLaps	optionDescriptionNumberOfLaps	0
			-2	lapsPlus2			""
			-1	lapsPlus1			""
			0	option_disabled		""
			1	laps_1				""
			3	laps_3				""
			5	laps_5				""
			10	laps_10				""
			15	laps_15				""
			20	laps_20				""
			50	laps_50				""
			100	laps_100			""
		end

		option	cvDelete		optionNameCVDelete	optionDescriptionCVDelete	0
			0	option_disabled	""
			1	option_enabled	""
		end

		option	defaultVehicle	optionNameVehicle	optionDescriptionVehicle	0
			0	optionVehicleMongoose	""
			1	optionVehicleWarthog	""
		end

		option	startGate		optionNameStartGate	optionDescriptionStartGate	1
			0	option_disabled		""
			1	optionStartGate1	""
			2	optionStartGate2	""
			3	optionStartGate3	""
			4	optionStartGate4	""
		end

	end

;	# Game Options #





;	# Variables #

	variables global
		networked	number	roundTime					0
		networked	number	bestLapSeconds				0
		networked	number	bestLapMilliseconds			0
		networked	number	bestLapNumber				0
		networked	number	frames						0
		local		number	host						0
		local		number	localCurrentLapSeconds		0
		local		number	localCurrentLapMilliseconds	0
		local		number	localLapFrames				0
		networked	number	startGateFrame				1
		networked	number	startGateDropped			0
		networked	number	raceStatus					0
		networked	object	firstCheckpoint				none
		networked	object	finalCheckpoint				none
		local		object	target						none
		networked	player	holeshotPlayer				none
		networked	player	bestLapPlayer				none
		local		player	client						none
	end


	variables player
		networked	number	lapInvalidated				1
		networked	number	lapFrames					0
		networked	number	currentLapSeconds			0
		networked	number	currentLapMilliseconds		0
		networked	number	onVehicle					0
		networked	number	currentCheckpoint			-1
		networked	object	vehicle						none
		local		object	local_target				none
		networked	timer	delay						2
	end


	variables object
		networked	number	order						0
	end

;	# Variables #





;	# Widgets #

	hud_widgets
		widget	bottom_left
	end

;	# Widgets #





;	# Stats #

	game_stats
		bestLapSeconds		number	empty	none	0	0	0
		bestLapMilliseconds	number	empty	none	0	0	0
		bestLapNumber		number	empty	none	0	0	0
		bestLapFrames		number	empty	none	0	0	0
	end

;	# Stats #





;	# Initialize #

	trigger initialization


		;	# Initialize Checkpoints

			action for_each race_checkpoint
				action set current_object.order set_to current_object.user_data
				action set current_object.team set_to attackers
				action object_set_invincibility current_object true
				action boundary_set_visible current_object no_one
			end

			;	# Set finalCheckpoint #
			action for_each race_checkpoint
				condition if finalCheckpoint == none or
				condition if current_object.order > finalCheckpoint.order
					action set finalCheckpoint = current_object
			end
			;	# Set finalCheckpoint #

			;	# Set firstCheckpoint #
			action for_each race_checkpoint
				condition if firstCheckpoint == none or
				condition if current_object.order < firstCheckpoint.order
					action set firstCheckpoint = current_object
			end
			;	# Set firstCheckpoint #

		;	# Initialize Checkpoints


		;	# Delete Competitive Version Objects #
			action for_each cv_delete
				condition if cvDelete == 1
					action delete_object current_object
			end
		;	# Delete Competitive Version Objects #


		;	# Hide Invisible Race Objects #
			action for_each race_hide
				action hide_object current_object true
			end
		;	# Hide Invisible Race Objects #


		;	# Cache the Initial Round Time Length #
		action set roundTime set_to round_timer
		;	# Cache the Initial Round Time Length #


		action set raceStatus set_to 1
		action set host set_to 1
	end

;	# Initialize #





;	# Basic Game Logic #


	;	# Delete Race Gates #
	trigger general
		condition if raceStatus == 1
			condition if startGateDropped == 0
				condition if frames > startGateFrame
					action for_each race_gate
						action delete_object current_object
					end
					action set raceStatus set_to 2
					action set frames set_to 0
					action set startGateDropped set_to 1
					action set round_timer set_to roundTime
	end
	;	# Delete Race Gates #


	;	# Delete Objects marked as race_delete #
		trigger race_delete
			action delete_object current_object
		end
	;	# Delete Objects marked as race_delete #


	;	# Delete Competitive Version Objects #
		trigger cv_delete
			condition if cvDelete == 1
				action delete_object current_object
		end
	;	# Delete Competitive Version Objects #


	;	# Hide Invisible Race Objects #
		trigger race_hide
			action hide_object current_object true
		end
	;	# Hide Invisible Race Objects #


	;	# Set Current Lap time #
	trigger player
		action set current_player.currentLapSeconds set_to current_player.lapFrames
		action set current_player.currentLapSeconds divide 60
		temporary number t current_player.currentLapSeconds
		action set t multiply 60
		action set current_player.currentLapMilliseconds set_to current_player.lapFrames
		action set current_player.currentLapMilliseconds subtract t
		action set current_player.currentLapMilliseconds multiply 1000
		action set current_player.currentLapMilliseconds divide 60
	end
	;	# Set Current Lap time #


	;	# Increment the frame counter #
	trigger general
		action set frames add 1
	end
	;	# Increment the frame counter #


	;	# Increment the player frame counters #
	trigger player
		condition if raceStatus == 2
			action set current_player.lapFrames add 1
	end
	;	# Increment the player frame counters #


	; # # If everyone has met the number of laps, end the round #
	trigger general
		condition if numberOfLaps > 0
			temporary number done 1
			action for_each player
				begin
					condition if current_player.score < numberOfLaps
						action set done set_to 0
				end
				begin
					condition if current_player.score >= numberOfLaps
						action delete_object current_player.vehicle
						action set current_player.score set_to current_player.bestLapFrames
						action set current_player.score multiply -1
				end
			end

			condition if done == 1
				action set raceStatus set_to 3
	end
	; # # If everyone has met the number of laps, end the round #


	; # If the round time limit has been exceeded, end the round #
	trigger general
		condition if roundTime > 0
			condition timer_expired round_timer
				action set raceStatus set_to 3

				action for_each player
					action set current_player.score set_to current_player.bestLapFrames
					action set current_player.score multiply -1
				end

	end
	; # If the round time limit has been exceeded, end the round #


;	# Basic Game Logic #





;	# Handle Vehicles #

	trigger player


		;	# Get players current Vehicle #
		temporary object vehicle none
		action player_get_vehicle current_player vehicle
		;	# Get players current Vehicle #


		;	# Check if Player is on vehicle #
		action set current_player.onVehicle set_to 0
		condition if vehicle != none
			action set current_player.onVehicle set_to 1
		;	# Check if Player is on vehicle #

	end


	;	# Spawn player in vehicle #
	trigger player

		condition if current_player.vehicle == none
			temporary object vehicle none
			begin
				condition if defaultVehicle == 0
					action create_object "mongoose" at current_player set vehicle "vehicle"
			end
			begin
				condition if defaultVehicle == 1
					action create_object "warthog" at current_player set vehicle "vehicle"
			end

			action set current_player.vehicle = vehicle
			action set vehicle.team = current_player.team

			condition not if client == none
				action player_set_vehicle current_player current_player.vehicle

	end
	;	# Spawn player in vehicle #

;	# Handle Vehicles #





;	# Handle UI #

	trigger player

		;	# Initialize UI #
		action player_set_objective current_player objective numberOfLaps
		;	# Initialize UI #


		;	# Display the players current lap time #
		action hud_widget_set_text widget widgetText local_player.currentLapSeconds	local_player.currentLapMilliseconds
		;	# Display the players current lap time #

	end

	trigger player
		action hud_widget_set_visibility widget current_player false
		temporary object biped current_player
		condition not if biped == none
			condition if raceStatus == 2
				action hud_widget_set_visibility widget current_player true
	end

;	# Handle UI #





;	# Handle Checkpoints #

	;	# Make sure all players have valid checkpoints #
	trigger player
		condition if current_player.currentCheckpoint < firstCheckpoint.order or
		condition if current_player.currentCheckpoint > finalCheckpoint.order
			action set current_player.currentCheckpoint set_to firstCheckpoint.order
	end
	;	# Make sure all players have valid checkpoints #


	;	# Set Checkpoint Visibility #
	trigger player
		condition if current_player.onVehicle == 1
			action for_each race_checkpoint
				action boundary_set_visible current_object no_one
				condition if current_object.order == current_player.currentCheckpoint
					;action boundary_set_visible current_object player current_player 1
			end
	end
	;	# Set Checkpoint Visibility #


	;	# The Player has hit the checkpoint #
	trigger player
		condition if current_player.onVehicle == 1
			action for_each race_holeshot
	            condition object_in_area current_player current_object

		            condition if holeshotPlayer == none
			            action set holeshotPlayer set_to current_player
			            action for_each player
				            action hud_post_message player current_player none holeshotMessage holeshotPlayer
			            end
			end
	end


	trigger player
		condition if current_player.onVehicle == 1
			action for_each race_checkpoint
				condition if current_object.order == current_player.currentCheckpoint
					condition object_in_area current_player current_object

						;	# Find next checkpoint #
						temporary number nextCheckpointNumber finalCheckpoint.order
						action set nextCheckpointNumber add 1
						action for_each race_checkpoint
							condition if current_object.order > current_player.currentCheckpoint
								condition if current_object.order < nextCheckpointNumber
									action set nextCheckpointNumber set_to current_object.order
						end
						;	# Find next checkpoint #


						;	# Update Player Current Checkpoint #
						action set current_player.currentCheckpoint set_to nextCheckpointNumber
						;	# Update Player Current Checkpoint #


						;	# Player has completed a lap #
						condition if current_player.currentCheckpoint > finalCheckpoint.order
							condition if raceStatus == 2
								temporary number bestPersonalLap	0
								temporary number bestOverallLap		0


								;	# Reset Player checkpoint counter #
								action set current_player.currentCheckpoint set_to firstCheckpoint.order
								;	# Reset Player checkpoint counter #


								;	# Increment Player Score #
								action set_score add 1 player current_player
								;	# Increment Player Score #


								;	# Set Player Best Lap #
								begin
									condition if current_player.lapInvalidated == 0
										condition if current_player.bestLapSeconds == 0
											condition if current_player.bestLapMilliseconds == 0
												action set bestPersonalLap set_to 1
												action set current_player.bestLapFrames set_to current_player.lapFrames
												action set current_player.bestLapFrames subtract 1
												action set current_player.bestLapSeconds set_to current_player.currentLapSeconds
												action set current_player.bestLapMilliseconds set_to current_player.currentLapMilliseconds
												action set current_player.bestLapNumber set_to current_player.score
								end
								begin
									condition if current_player.lapInvalidated == 0
										condition if current_player.currentLapSeconds < current_player.bestLapSeconds
											action set bestPersonalLap set_to 1
											action set current_player.bestLapFrames set_to current_player.lapFrames
											action set current_player.bestLapFrames subtract 1
											action set current_player.bestLapSeconds set_to current_player.currentLapSeconds
											action set current_player.bestLapMilliseconds set_to current_player.currentLapMilliseconds
											action set current_player.bestLapNumber set_to current_player.score
								end
								begin
									condition if current_player.lapInvalidated == 0
										condition if current_player.currentLapSeconds == current_player.bestLapSeconds
											condition if current_player.currentLapMilliseconds < current_player.bestLapMilliseconds
												action set bestPersonalLap set_to 1
												action set current_player.bestLapFrames set_to current_player.lapFrames
												action set current_player.bestLapFrames subtract 1
												action set current_player.bestLapSeconds set_to current_player.currentLapSeconds
												action set current_player.bestLapMilliseconds set_to current_player.currentLapMilliseconds
												action set current_player.bestLapNumber set_to current_player.score
								end
								;	# Set Player Best Lap #


								;	# Set Fastest Laptime #
								begin
									condition if current_player.lapInvalidated == 0
										condition if bestLapSeconds == 0
											condition if bestLapMilliseconds == 0
												action set bestOverallLap set_to 1
												action set bestLapSeconds set_to current_player.currentLapSeconds
												action set bestLapMilliseconds set_to current_player.currentLapMilliseconds
												action set bestLapPlayer set_to current_player
												action set bestLapNumber set_to current_player.score
								end
								begin
									condition if current_player.lapInvalidated == 0
										condition if current_player.currentLapSeconds < bestLapSeconds
											action set bestOverallLap set_to 1
											action set bestLapSeconds set_to current_player.currentLapSeconds
											action set bestLapMilliseconds set_to current_player.currentLapMilliseconds
											action set bestLapPlayer set_to current_player
											action set bestLapNumber set_to current_player.score
								end
								begin
									condition if current_player.lapInvalidated == 0
										condition if current_player.currentLapSeconds == bestLapSeconds
											condition if current_player.currentLapMilliseconds < bestLapMilliseconds
												action set bestOverallLap set_to 1
												action set bestLapSeconds set_to current_player.currentLapSeconds
												action set bestLapMilliseconds set_to current_player.currentLapMilliseconds
												action set bestLapPlayer set_to current_player
												action set bestLapNumber set_to current_player.score
								end
								;	# Set Fastest Laptime #


								;	# Send Lap Complete Notifications #
								begin
									condition if bestOverallLap == 1
										condition if current_player.lapInvalidated == 0
											action hud_post_message player current_player none overallBestLapMessage current_player.currentLapSeconds current_player.currentLapMilliseconds
								end
								begin
									condition if bestOverallLap != 1
										condition if bestPersonalLap == 1
											condition if current_player.lapInvalidated == 0
												action hud_post_message player current_player none personalBestLapMessage current_player.currentLapSeconds current_player.currentLapMilliseconds
								end
								begin
									condition if bestOverallLap != 1
										condition if bestPersonalLap != 1
											action hud_post_message player current_player none lapCompleteMessage	current_player.currentLapSeconds	current_player.currentLapMilliseconds
								end
								;action submit_incident lap_complete player current_player player none
								;	# Send Lap Complete Notifications #


								action set current_player.lapInvalidated set_to 0
								action set current_player.lapFrames set_to 1	; Reset lap frame count
						;	# Player has completed a lap #

			end
	end
	;	# The Player has hit the checkpoint #


;	# Handle Checkpoints #





	trigger player
		begin
			temporary object biped none
			action set biped set_to current_player
				condition if current_player.local_target equal_to none
					condition not if biped equal_to none
						condition not timer_expired current_player.delay
							action object_detach current_player.vehicle
							action create_object "monitor" at current_player set current_player.local_target label Target
							action object_attach current_player.local_target current_player 8 0 5
							action object_detach current_player.local_target
							action timer_set_rate current_player.delay 100
		end
	end





	trigger local


		;	# Detect Client Player #
		action for_each player
			begin
				condition timer_expired current_player.delay
					action for_each Target
						action delete_object current_object
						action player_set_vehicle current_player current_player.vehicle
					end
			end
			begin
				action player_get_target_object current_player target
				condition not if target equal_to none
					action set client set_to current_player
			end
			begin
				condition if client equal_to current_player

					;	# Hide Invisible Race Objects #
						action for_each race_hide
							action hide_object current_object true
						end
					;	# Hide Invisible Race Objects #


					condition if raceStatus == 2

						begin
							condition if host == 0


								;	# Set Current Local Lap time #
								action set localCurrentLapSeconds set_to localLapFrames
								action set localCurrentLapSeconds divide 60
								temporary number t localCurrentLapSeconds
								action set t multiply 60
								action set localCurrentLapMilliseconds set_to localLapFrames
								action set localCurrentLapMilliseconds subtract t
								action set localCurrentLapMilliseconds multiply 1000
								action set localCurrentLapMilliseconds divide 60
								;	# Set Current Local Lap time #


								;	# Display the players current lap time #
								begin
									condition if localCurrentLapMilliseconds >= 100
										action hud_widget_set_text widget widgetText localCurrentLapSeconds	localCurrentLapMilliseconds
								end
								begin
									condition if localCurrentLapMilliseconds < 100
										action hud_widget_set_text widget widgetText2 localCurrentLapSeconds	localCurrentLapMilliseconds
								end
								;	# Display the players current lap time #


								;	# Increment the player frame counters #
								action set localLapFrames add 1
								;	# Increment the player frame counters #


								;	# Player has completed a lap #
									condition if localLapFrames > 15
										condition if client.onVehicle == 1
											action for_each race_checkpoint
												condition if current_object.order == client.currentCheckpoint
													condition if current_object.order == finalCheckpoint.order
														condition object_in_area client current_object
															action set localLapFrames set_to 0
								;	# Player has completed a lap #

					end

			end
		end
		;	# Detect Client Player #

	end

