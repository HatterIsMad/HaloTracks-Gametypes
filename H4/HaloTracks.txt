include "strings/common_strings.txt"





;	# String Tables #

	string_table english
		engine_category_slayer				"Slayer"
		title								"Race"
		description							"Gentlemen, start your engines."
		objective							"Hit checkpoints to complete laps.\r\n%n laps to win."
		optionNameRounds		            "Rounds"
		optionDescriptionRounds         	"If enabled, time expiration or lap count will start a new round or end the game"
		optionNameWaypointVisibility		"Waypoint Visibility"
		optionDescriptionWaypointVisibility	"Show waypoints on the checkpoints"
		optionNameRequiredJokers			"Required Jokers"
		optionDescriptionRequiredJokers		"The number of jokers players must complete"
		optionNameNumberOfLaps				"Number of Laps"
		optionDescriptionNumberOfLaps		"The number of laps players must complete to win the race."
		optionNameVehicle					"Vehicle"
		optionDescriptionVehicle			"Determines what vehicle players will use in this race."
		optionVehicleMongoose				"Mongoose"
		optionVehicleWarthog				"Warthog"
		optionNameCVDelete					"Competitive"
		optionDescriptionCVDelete			"If enabled, objects with the label cv_delete will be deleted."
		optionNameStartGate					"Start Gate"
		optionDescriptionStartGate			"If enabled, objects with the label race_gate will disappear at the selected time."
		optionStartGate1					"20 +/- 5"
		optionStartGate2					"30 +/- 10"
		optionStartGate3					"40 +/- 20"
		optionStartGate4					"55 +/- 5"
		optionZero							"0"
		optionOne							"1"
		optionTwo							"2"
		optionThree							"3"
		lapsPlus1							"Time Limit + 1 Lap"
		lapsPlus2							"Time Limit + 2 Laps"
		empty								""
		widgetText							"Lap Time: %n.%n"
		widgetText2							"Lap Time: %n.0%n"
		finalLapMessage						"Final Lap."
		holeshotMessage						"%p got the holeshot"
		overallBestLapMessage				"Overall Best lap time! %n.%n"
		overallBestLapPlayerMessage			"Lap %n %p got the fastest lap"
		personalBestLapMessage				"Personal Best lap time! %n.%n"
		lapCompleteMessage					"Lap Complete: %n.%n"
		averageLapMessage               	"Average Lap: %n.%n"
		jokeredMessage						"You have completed %n/%n jokers"
		sector1Message						"Sector 1: %n.%n"
		overallBestSector1Message			"Overall Best Sector 1 time! %n.%n"
		personalBestSector1Message			"Personal Best Sector 1 time! %n.%n"
		sector2Message						"Sector 2: %n.%n"
		overallBestSector2Message			"Overall Best Sector 2 time! %n.%n"
		personalBestSector2Message			"Personal Best Sector 2 time! %n.%n"
		sector3Message						"Sector 3: %n.%n"
		overallBestSector3Message			"Overall Best Sector 3 time! %n.%n"
		personalBestSector3Message			"Personal Best Sector 3 time! %n.%n"
		optimalLapMessage                   "New Optimal Lap: %n.%n"
		whiteFlagMessage                    "White Flag"
		navpointRacePos						"%n P"
		debug								"%n"
	end

;	# String Tables #



;	# Engine Config #

	engine_data
		name title
		description description
		icon 7
		category slayer
	end

;	# Engine Config #





;	# Object Labels #

	map_object race_checkpoint
		label "race_checkpoint"
		min 1
	end

	map_object race_delete
		label "race_delete"
	end

	map_object race_holeshot
		label "race_holeshot"
	end

	map_object race_hide
		label "race_hide"
	end

	map_object race_gate
		label "race_gate"
	end

	map_object cv_delete
		label "cv_delete"
	end

	map_object vehicle
		label "vehicle"
	end

	map_object Target
		label "Target"
	end

	map_object joker
		label "joker"
	end

	map_object race_pos
		label "race_pos"
	end

;	# Object Labels #





;	# Game Options #

	loadout_palette empty_palette
	end

	game_options

		lock override	teams_enabled					0
		override		respawn_time					3
		override		suicide_respawn_penalty			3
		lock override	score_to_win_round				500
		override		round_count						1
		override		round_time_limit				60
		override		early_victory_win_count			0
		override		indestructible_vehicles			1
		override		grenades_on_map					0
		override		vehicle_set						no_vehicles

		override		loadout_selection_time			0
		override		loadout_palette spartan_tier1	empty_palette
		override		loadout_palette spartan_tier2	empty_palette
		override		loadout_palette spartan_tier3	empty_palette
		override		loadout_palette elite_tier1		empty_palette
		override		loadout_palette elite_tier2		empty_palette
		override		loadout_palette elite_tier3		empty_palette

		override		base_player_traits
						damage_multiplier				0
						initial_secondary_weapon		sticky_grenade_launcher
						initial_primary_weapon			magnum
						melee_damage_multiplier			0
						initial_grenade_count			none
						shield_hud						false
						initial_equipment				sprint_equipment
						assassination_immunity			true
						headshot_immunity				true
						damage_resistance				true
						vehicle_usage					not_passenger
						infinite_ammo					on
		end

		option	numberOfLaps	optionNameNumberOfLaps	optionDescriptionNumberOfLaps	0
			-2	lapsPlus2			""
			-1	lapsPlus1			""
			0	option_disabled		""
			1	laps_1				""
			3	laps_3				""
			5	laps_5				""
			10	laps_10				""
			15	laps_15				""
			20	laps_20				""
			50	laps_50				""
			100	laps_100			""
		end

		option	cvDelete		optionNameCVDelete	optionDescriptionCVDelete	0
			0	option_disabled	""
			1	option_enabled	""
		end

		option	defaultVehicle	optionNameVehicle	optionDescriptionVehicle	0
			0	optionVehicleMongoose	""
			1	optionVehicleWarthog	""
		end

		option	startGate		optionNameStartGate	optionDescriptionStartGate	1
			0	option_disabled		""
			1	optionStartGate1	""
			2	optionStartGate2	""
			3	optionStartGate3	""
			4	optionStartGate4	""
		end

		option	requiredJokers	optionNameRequiredJokers	optionDescriptionRequiredJokers	0
			0	option_disabled		""
			1	optionOne	""
			2	optionTwo	""
			3	optionThree	""
		end

		option	waypointVisibility	optionNameWaypointVisibility	optionDescriptionWaypointVisibility	0
			0	option_disabled	""
			1	option_enabled	""
		end

		option	rounds	optionNameRounds	optionDescriptionRounds	0
			0	option_disabled	""
			1	option_enabled	""
		end

	end

;	# Game Options #





;	# Variables #

	variables global
		networked	number	roundTime					0
		networked   number  bestLapFrames               0
		networked	number	frames						0
		local		number	host						0
		local		number	localCurrentLapSeconds		0
		local		number	localCurrentLapMilliseconds	0
		local		number	localLapFrames				0
		networked	number	startGateFrame				0
		networked	number	raceStatus					0
		networked	number	numberOfPlayers				0
		networked	number	checkpointCount				0
		networked	number	sectorCount					0
		networked	number	finalLap     				0
		networked	number	whiteFlag     				0
		networked	object	firstCheckpoint				none
		networked	object	finalCheckpoint				none
		local		object	target						none
		networked	player	holeshotPlayer				none
		networked	player	bestLapPlayer				none
		local		player	client						none
	end


	variables player
		networked	number	bestLapFrames				0
		networked	number	lapFrames					0
		networked	number	averageLapFrames			0
		networked	number	currentLapSeconds			0
		networked	number	currentLapMilliseconds		0
		networked	number	onVehicle					0
		networked	number	currentCheckpoint			-1
		networked	number	sectorTimeFrames			0
		networked	number	position					1
		networked	object	vehicle						none
		local		object	local_target				none
		networked	object	timing						none
		networked	timer	delay						2
	end


	variables object
		networked	number	order						0
		networked	number	optimalLapFrames    		0
		networked	number	jokeredThisLap				0
		networked	number	jokerCount					0
		networked	number	sector						0
		networked	number	sectorTimeFrames			0
		networked	number	bestSectorTimeFrames		0
		networked	number	bestSector2TimeFrames		0
		networked	number	bestSector3TimeFrames		0
		networked	number	sectorInvalidated			1
	end

;	# Variables #





;	# Widgets #

	hud_widgets
		widget	bottom_left
	end

;	# Widgets #





;	# Stats #

	game_stats
		stat1		number	empty	none	0	0	0
		stat2		number	empty	none	0	0	0
		stat3		number	empty	none	0	0	0
		stat4		number	empty	none	0	0	0
	end

;	# Stats #





;	# Initialize #

	trigger initialization


		;	# Initialize Checkpoints
			action for_each race_checkpoint

				action set current_object.order set_to current_object.user_data
				action set current_object.team set_to neutral
				action object_set_invincibility current_object true
				action navpoint_set_visible current_object no_one


				;	# Set Waypoint Type #
				begin
					condition if waypointVisibility == 1
						action NavPointSetType current_object navpoint_regicide
				end
				;	# Set Waypoint Type #



				;	# Set finalCheckpoint #
				begin
					condition if finalCheckpoint == none or
					condition if current_object.order > finalCheckpoint.order
						action set finalCheckpoint = current_object
				end
				;	# Set finalCheckpoint #


				;	# Set firstCheckpoint #
				begin
					condition if firstCheckpoint == none or
					condition if current_object.order < firstCheckpoint.order
						action set firstCheckpoint = current_object
				end
				;	# Set firstCheckpoint #

			end
		;	# Initialize Checkpoints


		;	# Count Distinct Checkpoints #
		begin
			action for_each race_checkpoint
				action for_each race_checkpoint
					condition if current_object.order == checkpointCount
						action set checkpointCount add 1
				end
			end
		end
		;	# Count Distinct Checkpoints #


		;	# Figure out Checkpoint Sectors #
		begin
			temporary object sector1 none
			temporary object sector2 none
			temporary object sector3 none

			begin
				condition if checkpointCount <= 3
					action for_each race_checkpoint
						begin
							condition if current_object.order == 0
								action set sector1 set_to current_object
						end
						begin
							condition if current_object.order == 1
								action set sector2 set_to current_object
						end
						begin
							condition if current_object.order == 2
								action set sector3 set_to current_object
						end
					end
			end


			temporary number spacing checkpointCount
			action set spacing divide 3
			temporary number order checkpointCount
			action set order subtract 1
			action set order subtract spacing

			begin
				condition if checkpointCount > 3
					action set sector3 set_to finalCheckpoint

					action for_each race_checkpoint
						condition if current_object.order == order
							action set sector2 set_to current_object
					end

					action set order subtract spacing

					action for_each race_checkpoint
						condition if current_object.order == order
							action set sector1 set_to current_object
					end
			end

			begin
				condition not if sector1 == none
					action set sectorCount set_to 1
			end

			begin
				condition not if sector2 == none
					action set sectorCount set_to 2
			end

			begin
				condition not if sector3 == none
					action set sectorCount set_to 3
			end


			begin
				action for_each race_checkpoint
					action set current_object.sector set_to 4

					begin
						condition not if sector1 == none
							condition if current_object.order == sector1.order
								action set current_object.sector set_to 1
					end

					begin
						condition not if sector2 == none
							condition if current_object.order == sector2.order
								action set current_object.sector set_to 2
					end

					begin
						condition not if sector3 == none
							condition if current_object.order == sector3.order
								action set current_object.sector set_to 3
					end
				end
			end

		end
		;	# Figure out Checkpoint Sectors #


		;	# Delete Competitive Version Objects #
			action for_each cv_delete
				condition if cvDelete == 1
					action delete_object current_object
			end
		;	# Delete Competitive Version Objects #


		;	# Hide Invisible Race Objects #
			action for_each race_hide
				action hide_object current_object true
			end
		;	# Hide Invisible Race Objects #


		;	# Set Start Gate Frame #
		begin
			condition if startGate == 1 ; 20 +/- 5
				action random 571 startGateFrame
				action set startGateFrame add 900
		end
		begin
			condition if startGate == 2 ; 30 +/- 10
				action random 1171 startGateFrame
				action set startGateFrame add 1200
		end
		begin
			condition if startGate == 3 ; 40 +/- 20
				action random 2371 startGateFrame
				action set startGateFrame add 1200
		end
		begin
			condition if startGate == 4 ; 55 +/- 5
				action random 571 startGateFrame
				action set startGateFrame add 3000
		end
		action set startGateFrame add 600 ; Add 10s for the initial H4 spawn
		;	# Set Start Gate Frame #


		;	# Cache the Initial Round Time Length #
		action set roundTime set_to round_timer
		;	# Cache the Initial Round Time Length #


		action set raceStatus set_to 1
		action set host set_to 1
	end

;	# Initialize #





;	# Basic Game Logic #

	;	# Delete Race Gates #
	trigger general
		condition if raceStatus == 1
		    condition if frames > startGateFrame
				action for_each race_gate
					action delete_object current_object
				end
				action set raceStatus set_to 2
				action set frames set_to 0
				action set round_timer set_to roundTime
	end
	;	# Delete Race Gates #


	;	# Count number of alive players #
	trigger general
		action set numberOfPlayers set_to 0

		action for_each player
			temporary object biped current_player
			condition not if biped == none
				action set numberOfPlayers add 1
		end
	end
	;	# Count number of alive players #


	;	# Delete Objects marked as race_delete #
		trigger race_delete
			action delete_object current_object
		end
	;	# Delete Objects marked as race_delete #


	;	# Delete Competitive Version Objects #
		trigger cv_delete
			condition if cvDelete == 1
				action delete_object current_object
		end
	;	# Delete Competitive Version Objects #


	;	# Hide Invisible Race Objects #
		trigger race_hide
			action hide_object current_object true
		end
	;	# Hide Invisible Race Objects #


	;	# Set Current Lap time #
	trigger player
		action set current_player.currentLapSeconds set_to current_player.lapFrames
		action set current_player.currentLapSeconds divide 60
		temporary number t current_player.currentLapSeconds
		action set t multiply 60
		action set current_player.currentLapMilliseconds set_to current_player.lapFrames
		action set current_player.currentLapMilliseconds subtract t
		action set current_player.currentLapMilliseconds multiply 1000
		action set current_player.currentLapMilliseconds divide 60
	end
	;	# Set Current Lap time #


	;	# Increment the frame counter #
	trigger general
		action set frames add 1
	end
	;	# Increment the frame counter #


	;	# Increment the player frame counters #
	trigger player
		condition if raceStatus == 2
			temporary object timing current_player.timing
			action set timing.sectorTimeFrames add 1
			action set current_player.lapFrames add 1
	end
	;	# Increment the player frame counters #


	;	# End the round when time runs out #
	trigger general
		condition if roundTime > 0
			condition timer_expired round_timer
				begin
					condition if rounds == 1
						action end_round
				end
				begin
					condition if rounds == 0
						condition if numberOfLaps < 0
							condition if finalLap == 0
								action for_each player
									condition if current_player.score > finalLap
										action set finalLap set_to current_player.score
								end
								begin
									condition if numberOfLaps == -1
										action set finalLap add 2
								end
								begin
									condition if numberOfLaps == -2
										action set finalLap add 3
								end
				end
	end
	;	# End the round when time runs out #


	;	# End the round if anyone has met the finalLap
	trigger player
		condition if roundTime > 0
			condition timer_expired round_timer
				begin
					temporary number whiteFlagLap finalLap
					action set whiteFlagLap subtract 1
					condition if rounds == 0
						condition if finalLap != 0
							condition if current_player.position == 1
							     condition if current_player.score == whiteFlagLap
							         condition if whiteFlag == 0
							             action set whiteFlag set_to 1
							             action for_each player
								             action hud_post_message player current_player none whiteFlagMessage
								         end
				end
	end
	;	# End the round if anyone has met the finalLap


	;	# End the round if anyone has met the numberOfLaps
	trigger player
		condition if rounds == 1
			condition if numberOfLaps > 0
				condition if current_player.score >= numberOfLaps
					action end_round
	end
	;	# End the round if anyone has met the numberOfLaps


	;	# Set Stats
	trigger player

	end
	;	# Set Stats


	;	# Create Objects for storage #
	trigger player
		condition if current_player.timing == none
			action create_object "flag" at finalCheckpoint set current_player.timing label race_hide
			action hide_object current_player.timing true
	end
	;	# Create Objects for storage #


	;	# Set Player current racing position #
	trigger player
		temporary player racer current_player
		temporary number position 1

		;	# Find out how many players are ahead #
		action for_each player

			begin
				condition if current_player.score > racer.score
					action set position add 1
			end


			condition if current_player.score == racer.score

				begin
					condition if current_player.currentCheckpoint > racer.currentCheckpoint
						action set position add 1
				end

				condition if current_player.currentCheckpoint == racer.currentCheckpoint
					condition if racer.sectorTimeFrames < current_player.sectorTimeFrames
						action set position add 1

		end
		;	# Find out how many players are ahead #

		action set current_player.position set_to position
	end
	;	# Set Player current racing position #


;	# Basic Game Logic #





;	# Handle Vehicles #

	trigger player


		;	# Get players current Vehicle #
		temporary object vehicle none
		action player_get_vehicle current_player vehicle
		;	# Get players current Vehicle #


		;	# Check if Player is on vehicle #
		action set current_player.onVehicle set_to 0
		condition if vehicle != none
			action set current_player.onVehicle set_to 1
		;	# Check if Player is on vehicle #

	end


	;	# Spawn player in vehicle #
	trigger player

		condition if current_player.vehicle == none
			temporary object vehicle none
			begin
				condition if defaultVehicle == 0
					action create_object "mongoose" at current_player set vehicle label race_pos "vehicle"
			end
			begin
				condition if defaultVehicle == 1
					action create_object "warthog" at current_player set vehicle label race_pos "vehicle"
			end

			action set current_player.vehicle = vehicle
			action set vehicle.team = current_player.team

			condition not if client == none
				action player_set_vehicle current_player current_player.vehicle

	end
	;	# Spawn player in vehicle #

;	# Handle Vehicles #





;	# Handle UI #

	trigger player

		;	# Initialize UI #
		action player_set_objective current_player objective numberOfLaps
		;	# Initialize UI #


		;	# Display the players current lap time #
		action hud_widget_set_text widget widgetText local_player.currentLapSeconds	local_player.currentLapMilliseconds
		;	# Display the players current lap time #

	end

	trigger player
		action hud_widget_set_visibility widget current_player false
		temporary object biped current_player
		condition not if biped == none
			condition if raceStatus == 2
				action hud_widget_set_visibility widget current_player true
	end

	;	# Handle navpoint widget #
	trigger player
		condition if current_player.onVehicle == 1
			condition if raceStatus == 2
				condition if holeshotPlayer != none
					temporary object vehicle current_player.vehicle
						action NavPointSetType vehicle navpoint_megalo_message_hostile ;This navpoint type is rendered as an additional text widget in the player's UI.
						action navpoint_set_visible vehicle player current_player true
						action set vehicle.team set_to attackers ;Setting the navpoint's team to attackers ensures it presents user-defined text.
	end
	;	# Handle navpoint widget #

;	# Handle UI #





;	# Handle Checkpoints #

	;	# Make sure all players have valid checkpoints #
	trigger player
		condition if current_player.currentCheckpoint < firstCheckpoint.order or
		condition if current_player.currentCheckpoint > finalCheckpoint.order
			action set current_player.currentCheckpoint set_to firstCheckpoint.order
	end
	;	# Make sure all players have valid checkpoints #


	;	# Set Checkpoint Visibility #
	trigger player
		condition if current_player.onVehicle == 1
			condition if waypointVisibility == 1
				action for_each race_checkpoint
					action navpoint_set_visible current_object player current_player 0
					condition if current_object.order == current_player.currentCheckpoint
						action navpoint_set_visible current_object player current_player 1
				end
	end
	;	# Set Checkpoint Visibility #


	;	# The Player has hit the checkpoint #
	trigger player
		condition if current_player.onVehicle == 1
			action for_each race_holeshot
	            condition object_in_area current_player current_object

		            condition if holeshotPlayer == none
			            action set holeshotPlayer set_to current_player
						condition if numberOfPlayers != 1
							action for_each player
								action hud_post_message player current_player none holeshotMessage holeshotPlayer
							end
			end
	end


	trigger player
		condition if current_player.onVehicle == 1

			;	# Get Player Timing #
			temporary object timing current_player.timing
			;	# Get Player Timing #


			;	# Handle Jokers #
			begin
			 condition if requiredJokers > 0
				action for_each joker

					;	# Set Waypoint Visibility #
					begin
						condition if waypointVisibility == 1
							action NavPointSetType current_object navpoint_regicide
					end

					begin
						condition if timing.jokeredThisLap == 1 or
						condition if current_object.order != current_player.currentCheckpoint or
						condition if timing.jokerCount >= requiredJokers
							action navpoint_set_visible current_object player current_player 0
					end

					begin
						condition if raceStatus >= 2
							condition if timing.jokerCount < requiredJokers
								condition if timing.jokeredThisLap == 0
									condition if current_object.order == current_player.currentCheckpoint
										begin
											condition if waypointVisibility == 1
												action navpoint_set_visible current_object player current_player 1
										end
					end
					;	# Set Waypoint Visibility #

					begin
						condition if raceStatus >= 2
							condition if timing.jokerCount < requiredJokers
								condition if timing.jokeredThisLap == 0
									condition if current_object.order == current_player.currentCheckpoint
										condition object_in_area current_player current_object
											action set timing.jokerCount add 1
											action set timing.jokeredThisLap set_to 1
											action hud_post_message player current_player none jokeredMessage timing.jokerCount requiredJokers
					end
				end
			end
			;	# Handle Jokers #



			action for_each race_checkpoint
				condition if current_object.order == current_player.currentCheckpoint
					condition object_in_area current_player current_object


						;	# Handle Sector Times #
						begin
							condition if timing.sectorTimeFrames != 0
								temporary number sectorTimeFrames timing.sectorTimeFrames
								temporary number bestOverallSector 0
								temporary number bestPersonalSector 0

								;	# Calculate Lap Time #
								temporary number framesIn sectorTimeFrames
								temporary number secondsOut framesIn
								action set secondsOut divide 60
								temporary number t secondsOut
								action set t multiply 60
								temporary number millisecondsOut framesIn
								action set millisecondsOut subtract t
								action set millisecondsOut multiply 1000
								action set millisecondsOut divide 60
								;	# Calculate Lap Time #



								begin
									condition if current_object.sector == 1
										begin
											condition if timing.sectorInvalidated == 0
												begin
													condition if timing.bestSectorTimeFrames == 0 or
													condition if timing.bestSectorTimeFrames > sectorTimeFrames
														action set timing.bestSectorTimeFrames set_to sectorTimeFrames
														action set bestPersonalSector set_to 1
												end
												begin
													condition if current_object.bestSectorTimeFrames == 0 or
													condition if current_object.bestSectorTimeFrames > sectorTimeFrames
														action set current_object.bestSectorTimeFrames set_to sectorTimeFrames
														action set bestOverallSector set_to 1
												end
										end


										;	# Send Sector Complete Notifications #
										begin
											condition if bestOverallSector == 1
												action hud_post_message player current_player none overallBestSector1Message secondsOut millisecondsOut
										end
										begin
											condition if bestOverallSector != 1
												condition if bestPersonalSector == 1
													action hud_post_message player current_player none personalBestSector1Message secondsOut millisecondsOut
										end
										begin
											condition if bestOverallSector != 1
												condition if bestPersonalSector != 1
													action hud_post_message player current_player none sector1Message secondsOut millisecondsOut
										end
										;	# Send Sector Complete Notifications #

										action set timing.sectorTimeFrames set_to 0		; sectorStartFrame -- Set Frame number the user started the current sector on
										action set timing.sectorInvalidated set_to 0
								end


								begin
									condition if current_object.sector == 2
										begin
											condition if timing.sectorInvalidated == 0
												begin
													condition if timing.bestSector2TimeFrames == 0 or
													condition if timing.bestSector2TimeFrames > sectorTimeFrames
														action set timing.bestSector2TimeFrames set_to sectorTimeFrames
														action set bestPersonalSector set_to 1
												end
												begin
													condition if current_object.bestSectorTimeFrames == 0 or
													condition if current_object.bestSectorTimeFrames > sectorTimeFrames
														action set current_object.bestSectorTimeFrames set_to sectorTimeFrames
														action set bestOverallSector set_to 1
												end
										end


										;	# Send Sector Complete Notifications #
										begin
											condition if bestOverallSector == 1
												action hud_post_message player current_player none overallBestSector2Message secondsOut millisecondsOut
										end
										begin
											condition if bestOverallSector != 1
												condition if bestPersonalSector == 1
													action hud_post_message player current_player none personalBestSector2Message secondsOut millisecondsOut
										end
										begin
											condition if bestOverallSector != 1
												condition if bestPersonalSector != 1
													action hud_post_message player current_player none sector2Message secondsOut millisecondsOut
										end
										;	# Send Sector Complete Notifications #

										action set timing.sectorTimeFrames set_to 0		; sectorStartFrame -- Set Frame number the user started the current sector on
										action set timing.sectorInvalidated set_to 0
								end


								begin
									condition if current_object.sector == 3
										begin
											condition if timing.sectorInvalidated == 0
												begin
													condition if timing.bestSector3TimeFrames == 0 or
													condition if timing.bestSector3TimeFrames > sectorTimeFrames
														action set timing.bestSector3TimeFrames set_to sectorTimeFrames
														action set bestPersonalSector set_to 1
												end
												begin
													condition if current_object.bestSectorTimeFrames == 0 or
													condition if current_object.bestSectorTimeFrames > sectorTimeFrames
														action set current_object.bestSectorTimeFrames set_to sectorTimeFrames
														action set bestOverallSector set_to 1
												end
										end


										;	# Send Sector Complete Notifications #
										begin
											condition if bestOverallSector == 1
												action hud_post_message player current_player none overallBestSector3Message secondsOut millisecondsOut
										end
										begin
											condition if bestOverallSector != 1
												condition if bestPersonalSector == 1
													action hud_post_message player current_player none personalBestSector3Message secondsOut millisecondsOut
										end
										begin
											condition if bestOverallSector != 1
												condition if bestPersonalSector != 1
													action hud_post_message player current_player none sector3Message secondsOut millisecondsOut
										end
										;	# Send Sector Complete Notifications #

										action set timing.sectorTimeFrames set_to 0		; sectorStartFrame -- Set Frame number the user started the current sector on
										action set timing.sectorInvalidated set_to 0
								end

						end
						;	# Handle Sector Times #


                        ;   # Update Optimal Laptime #
                        begin

                            ;	# Calculate Lap Time #
							temporary number framesIn timing.bestSectorTimeFrames
                            action set framesIn add timing.bestSector2TimeFrames
                            action set framesIn add timing.bestSector3TimeFrames
                            temporary number secondsOut framesIn
                            action set secondsOut divide 60
                            temporary number t secondsOut
                            action set t multiply 60
                            temporary number millisecondsOut framesIn
                            action set millisecondsOut subtract t
                            action set millisecondsOut multiply 1000
                            action set millisecondsOut divide 60
                            ;	# Calculate Lap Time #


                            temporary number oldOptimalFrames timing.optimalLapFrames
                            action set timing.optimalLapFrames set_to framesIn


                            begin
                                condition if sectorCount >= 1
                                    condition if timing.bestSectorTimeFrames == 0
                                        action set timing.optimalLapFrames set_to 0
                            end
                            begin
                                condition if sectorCount >= 2
                                    condition if timing.bestSectorTimeFrames == 0
                                        action set timing.optimalLapFrames set_to 0
                            end
                            begin
                                condition if sectorCount >= 3
                                    condition if timing.bestSectorTimeFrames == 0
                                        action set timing.optimalLapFrames set_to 0
                            end


                            condition if timing.optimalLapFrames != 0
                                condition if timing.optimalLapFrames != oldOptimalFrames
                                    action hud_post_message player current_player none optimalLapMessage secondsOut millisecondsOut


                        end
                        ;   # Update Optimal Laptime #


						;	# Find next checkpoint #
						temporary number nextCheckpointNumber finalCheckpoint.order
						action set nextCheckpointNumber add 1
						action for_each race_checkpoint
							condition if current_object.order > current_player.currentCheckpoint
								condition if current_object.order < nextCheckpointNumber
									action set nextCheckpointNumber set_to current_object.order
						end
						;	# Find next checkpoint #


						;	# Update Player Current Checkpoint #
						action set current_player.currentCheckpoint set_to nextCheckpointNumber
						;	# Update Player Current Checkpoint #


						;	# Player has completed a lap #
						condition if current_player.currentCheckpoint > finalCheckpoint.order
							condition if raceStatus == 2
								temporary number bestPersonalLap	0
								temporary number bestOverallLap		0


								;	# Reset Player checkpoint counter #
								action set current_player.currentCheckpoint set_to firstCheckpoint.order
								;	# Reset Player checkpoint counter #


								;	# Increment Player Score #
								action set_score add 1 player current_player
								;	# Increment Player Score #


								;   # Update Average Lap time #
								begin
									condition if current_player.score > 2
								        temporary number score current_player.score
								        action set score subtract 1
										action set current_player.averageLapFrames add current_player.lapFrames
										temporary number averageLapFrames current_player.averageLapFrames
										action set averageLapFrames divide score
										action set averageLapFrames add 1
										temporary number averageLapSeconds averageLapFrames
								        action set averageLapSeconds divide 60
								        temporary number t averageLapSeconds
								        action set t multiply 60
										temporary number averageLapMilliseconds averageLapFrames
								        action set averageLapMilliseconds subtract t
								        action set averageLapMilliseconds multiply 1000
								        action set averageLapMilliseconds divide 60
								        condition if current_player.score > 2
											action hud_post_message player current_player none averageLapMessage averageLapSeconds averageLapMilliseconds
								end
								;   # Update Average Lap time #


								;	# Set Player Best Lap #
								begin
									condition if current_player.score > 1
									    condition if current_player.bestLapFrames == 0 or
									    condition if current_player.lapFrames < current_player.bestLapFrames
												action set bestPersonalLap set_to 1
												action set current_player.bestLapFrames set_to current_player.lapFrames
												action set current_player.bestLapFrames subtract 1
								end
								;	# Set Player Best Lap #


								;	# Set Fastest Laptime #
								begin
									condition if current_player.score > 1
									    condition if bestLapFrames == 0 or
									    condition if current_player.lapFrames < bestLapFrames
											action set bestOverallLap set_to 1
											action set bestLapFrames set_to current_player.lapFrames
											action set bestLapPlayer set_to current_player
								end
								;	# Set Fastest Laptime #


								;	# Send Lap Complete Notifications #
								begin
									condition if bestOverallLap == 1
										condition if current_player.score > 1
											action hud_post_message player current_player none overallBestLapMessage current_player.currentLapSeconds current_player.currentLapMilliseconds
								end
								begin
									condition if bestOverallLap != 1
										condition if bestPersonalLap == 1
											condition if current_player.score > 1
												action hud_post_message player current_player none personalBestLapMessage current_player.currentLapSeconds current_player.currentLapMilliseconds
								end
								begin
									condition if bestOverallLap != 1
										condition if bestPersonalLap != 1
											action hud_post_message player current_player none lapCompleteMessage	current_player.currentLapSeconds	current_player.currentLapMilliseconds
								end
								;action submit_incident lap_complete player current_player player none
								;	# Send Lap Complete Notifications #


								action set timing.jokeredThisLap set_to 0	; Reset joker flag
								action set current_player.lapFrames set_to 1	; Reset lap frame count
						;	# Player has completed a lap #


						;	# Start the race when someone hits a checkpoint #
						condition if raceStatus < 2
							action set raceStatus set_to 2
							action set current_player.lapFrames set_to 1
							action set frames set_to 0
						;	# Start the race when someone hits a checkpoint #

			end
	end
	;	# The Player has hit the checkpoint #


;	# Handle Checkpoints #





	trigger player
		begin
			temporary object biped none
			action set biped set_to current_player
				condition if current_player.local_target equal_to none
					condition not if biped equal_to none
						condition not timer_expired current_player.delay
							action object_detach current_player.vehicle
							action create_object "monitor" at current_player set current_player.local_target label Target
							action object_attach current_player.local_target current_player 8 0 5
							action object_detach current_player.local_target
							action timer_set_rate current_player.delay 100
		end
	end





	trigger local


		;	# Detect Client Player #
		action for_each player
			begin
				condition timer_expired current_player.delay
					action for_each Target
						action delete_object current_object
						action player_set_vehicle current_player current_player.vehicle
					end
			end
			begin
				action player_get_target_object current_player target
				condition not if target equal_to none
					action set client set_to current_player
			end
			begin
				condition if client equal_to current_player

					;	# Hide Invisible Race Objects #
						action for_each race_hide
							action hide_object current_object true
						end
					;	# Hide Invisible Race Objects #


					condition if raceStatus == 2

	                    ;	# Handle navpoint widget #
	                    begin
		                        condition if current_player.onVehicle == 1
				                        condition if holeshotPlayer != none
					                        temporary object vehicle current_player.vehicle
					                        action navpoint_set_secondary_text vehicle navpointRacePos current_player.position
	                    end
	                    ;	# Handle navpoint widget #

						begin
							condition if host == 0


								;	# Set Current Local Lap time #
								action set localCurrentLapSeconds set_to localLapFrames
								action set localCurrentLapSeconds divide 60
								temporary number t localCurrentLapSeconds
								action set t multiply 60
								action set localCurrentLapMilliseconds set_to localLapFrames
								action set localCurrentLapMilliseconds subtract t
								action set localCurrentLapMilliseconds multiply 1000
								action set localCurrentLapMilliseconds divide 60
								;	# Set Current Local Lap time #


								;	# Display the players current lap time #
								begin
									condition if localCurrentLapMilliseconds >= 100
										action hud_widget_set_text widget widgetText localCurrentLapSeconds	localCurrentLapMilliseconds
								end
								begin
									condition if localCurrentLapMilliseconds < 100
										action hud_widget_set_text widget widgetText2 localCurrentLapSeconds	localCurrentLapMilliseconds
								end
								;	# Display the players current lap time #


								;	# Increment the player frame counters #
								action set localLapFrames add 1
								;	# Increment the player frame counters #


								;	# Player has completed a lap #
									condition if localLapFrames > 15
										condition if client.onVehicle == 1
											action for_each race_checkpoint
												condition if current_object.order == client.currentCheckpoint
													condition if current_object.order == finalCheckpoint.order
														condition object_in_area client current_object
															action set localLapFrames set_to 0
								;	# Player has completed a lap #

					end

			end
		end
		;	# Detect Client Player #

	end