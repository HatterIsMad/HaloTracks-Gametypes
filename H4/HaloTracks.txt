include "strings/common_strings.txt"





;	# String Tables #

	string_table english
		engine_category_slayer			"Slayer"
		title							"Race"
		description						"Gentlemen, start your engines."
		objective						"Hit checkpoints to complete laps.\r\n%n laps to win."
		optionNameNumberOfLaps			"Number of Laps"
		optionDescriptionNumberOfLaps	"Determines the number of laps players must attempt to complete to win the race."
		optionNameVehicle				"Vehicle"
		optionDescriptionVehicle		"Determines what vehicle players will use in this race."
		optionVehicleMongoose			"Mongoose"
		optionVehicleWarthog			"Warthog"
		optionNameCVDelete				"Competitive"
		optionDescriptionCVDelete		"If enabled, objects with the label cv_delete will be deleted."
		statBestLap						"Best Lap"
		widgetText						"%n.%n"
		lapCompleteMessage				"Lap Complete."
		bestLapMessage					"New best lap time!"
		finalLapMessage					"Final Lap."
		debug							"%n"
	end

;	# String Tables #





;	# Engine Config #

	engine_data
		name title
		description description
		icon 7
		category slayer
	end

;	# Engine Config #





;	# Object Labels #

	map_object race_flag
		label "race_checkpoint"
		min 1
	end

	map_object race_delete
		label "race_delete"
	end

	map_object race_hide
		label "race_hide"
	end

	map_object race_gate
		label "race_gate"
	end

	map_object cv_delete
		label "cv_delete"
	end

	map_object vehicle
		label "vehicle"
	end


;	# Object Labels #





;	# Game Options #

	loadout_palette empty_palette
	end

	game_options

		lock override	teams_enabled					0
		override		respawn_time					3
		override		suicide_respawn_penalty			3
		lock override	score_to_win_round				500
		override		round_count						1
		override		round_time_limit				10
		override		early_victory_win_count			0
		override		indestructible_vehicles			1
		override		grenades_on_map					0
		override		vehicle_set						no_vehicles

		override		loadout_selection_time			0
		override		loadout_palette spartan_tier1	empty_palette
		override		loadout_palette spartan_tier2	empty_palette
		override		loadout_palette spartan_tier3	empty_palette
		override		loadout_palette elite_tier1		empty_palette
		override		loadout_palette elite_tier2		empty_palette
		override		loadout_palette elite_tier3		empty_palette

		override		base_player_traits
						shield_hud						false
						initial_equipment				sprint_equipment
						assassination_immunity			true
						damage_resistance				20.00
						vehicle_usage					not_passenger
		end

		option	numberOfLaps	optionNameNumberOfLaps	optionDescriptionNumberOfLaps	3
			1	laps_1		""
			3	laps_3		""
			5	laps_5		""
			10	laps_10		""
			15	laps_15		""
			20	laps_20		""
			50	laps_50		""
			100	laps_100	""
		end

		option	cvDelete	optionNameCVDelete	optionDescriptionCVDelete	0
			0	option_disabled	""
			1	option_enabled	""
		end

		option	defaultVehicle	optionNameVehicle	optionDescriptionVehicle	0
			0	optionVehicleMongoose	""
			1	optionVehicleWarthog	""
		end

	end

;	# Game Options #





;	# Variables #

	variables global
		networked	number	frames					0
		networked	number	raceStatus				0
		networked	object	firstCheckpoint			none
		networked	object	finalCheckpoint			none
	end


	variables player
		networked	number	lapFrames				0
		networked	number	currentLapSeconds		0
		networked	number	currentLapMilliseconds	0
		networked	number	onVehicle				0
		networked	object	vehicle					none


		networked	number	target_gate				-1
		networked	number	lap						0
		networked	number	lap_complete			0
		local		number	legal					1
		networked	timer	on_foot					10
		networked	timer	lap_time				0
	end


	variables object
		networked	number	order					0
		networked	timer	deathrow				16
		networked	player	rider					none
	end

;	# Variables #





;	# Widgets #

	hud_widgets
		widget	bottom_left
	end

;	# Widgets #





;	# Stats #

	game_stats
		stat_best_lap_time	timer	statBestLap	none	0	0	0
	end

;	# Stats #





;	# Initialize #

	trigger initialization


		;	# Initialize Checkpoints

			action for_each race_flag
				action set current_object.order set_to current_object.user_data
				action set current_object.team set_to attackers
				action object_set_invincibility current_object true
				action boundary_set_visible current_object no_one
			end

			;	# Set firstCheckpoint #
			action for_each race_flag
				condition if firstCheckpoint == none or
				condition if current_object.order > firstCheckpoint.order
					action set firstCheckpoint = current_object
			end
			;	# Set firstCheckpoint #

			;	# Set finalCheckpoint #
			action for_each race_flag
				condition if finalCheckpoint == none or
				condition if current_object.order < finalCheckpoint.order
					action set finalCheckpoint = current_object
			end
			;	# Set finalCheckpoint #

		;	# Initialize Checkpoints


		;	# Delete Competitive Version Objects #
			action for_each cv_delete
				condition if cvDelete == 1
					action delete_object current_object
			end
		;	# Delete Competitive Version Objects #


		;	# Hide Invisible Race Objects #
			action for_each race_hide
				action hide_object current_object true
			end
		;	# Hide Invisible Race Objects #


		action set raceStatus set_to 2 ; 1
	end

;	# Initialize #





;	# Basic Game Logic #


	;	# Set Current Lap time #
	trigger player
		action set current_player.currentLapSeconds set_to current_player.lapFrames
		action set current_player.currentLapSeconds divide 60
		temporary number t current_player.currentLapSeconds
		action set t multiply 60
		action set current_player.currentLapMilliseconds set_to current_player.lapFrames
		action set current_player.currentLapMilliseconds subtract t
		action set current_player.currentLapMilliseconds multiply 1000
		action set current_player.currentLapMilliseconds divide 60
	end
	;	# Set Current Lap time #


	;	# Increment the frame counter #
	trigger general
		action set frames add 1
	end
	;	# Increment the frame counter #


	;	# Increment the player frame counters #
	trigger player
		condition if raceStatus >= 2
			action set current_player.lapFrames add 1
	end
	;	# Increment the player frame counters #


;	# Basic Game Logic #





;	# Handle Vehicles #

	trigger player


		;	# Get players current Vehicle #
		temporary object vehicle none
		action player_get_vehicle current_player vehicle
		;	# Get players current Vehicle #


		;	# Check if Player is on vehicle #
		action set current_player.onVehicle set_to 0
		condition if vehicle != none
			action set current_player.onVehicle set_to 1
		;	# Check if Player is on vehicle #


	end

		;	# Increment the player frame counters #
	trigger player
		action hud_post_message player current_player none debug	current_player.onVehicle
	end
	;	# Increment the player frame counters #

;	# Handle Vehicles #





;	# Handle UI #

	trigger player

		;	# Initialize UI #
		action player_set_objective current_player objective numberOfLaps
		;	# Initialize UI #

		action hud_widget_set_text widget widgetText local_player.currentLapSeconds	local_player.currentLapMilliseconds

	end

	trigger player
		action hud_widget_set_visibility widget current_player false
		temporary object biped current_player
		condition not if biped equal_to none
			action hud_widget_set_visibility widget current_player true
	end

;	# Handle UI #





;	# Handle Checkpoints #
;	# Handle Checkpoints #





trigger local

	action for_each player



	end

end










trigger race_flag

	action boundary_set_visible current_object no_one

	action for_each player
		condition if current_object.order equal_to current_player.target_gate
		action boundary_set_visible current_object player current_player 1
	end

end



; # make sure you have a vehicle to start #
trigger player

	condition if current_player.vehicle == none
	temporary object vehicle none
	action for_each general
		condition if defaultVehicle == 0
		action create_object "mongoose" at current_player set vehicle "vehicle"
	end

	action for_each general
		condition if defaultVehicle == 1
		action create_object "warthog" at current_player set vehicle "vehicle"
	end

	action set current_player.vehicle = vehicle
	action set vehicle.rider = current_player
	action set vehicle.team = current_player.team

	action player_set_vehicle current_player current_player.vehicle

end
; # make sure you have a vehicle to start #


; # make sure your vehicle doesn't get deleted out from under you #
trigger player

	temporary object vehicle none
	action player_get_vehicle current_player vehicle
	condition not if vehicle == none
	action set current_player.vehicle = vehicle
	action set vehicle.rider = current_player

end
; # make sure your vehicle doesn't get deleted out from under you #


; need to move the target gate as you hit it
; score 1 point for hitting target gate

trigger player

	action player_set_coop_spawning current_player 1
	action set_respawn_filter current_player no_one	 	;no one should be able to bro spawn on me!


	temporary number checkpoint_reached 0

	condition if current_player.legal == 1	;must be in a vehicle for the checkpoint to count

	action for_each race_flag
		condition if current_object.order equal_to current_player.target_gate

		action navpoint_set_visible current_object player current_player 1

		; And if the player has arrived
		condition object_in_area current_player current_object

		; Clear the nav point, flag the checkpoint as reached
		action navpoint_set_visible current_object player current_player 0
		action set checkpoint_reached = 1
	end


	;tysongr - Race robustness. Find the NEXT HIGHEST GATE NUMBER, give that to the player as their next target.
	; The starting point is the highest recorded gate number + 1
	temporary number previous_order current_player.target_gate
	temporary number next_order finalCheckpoint.order
	action set next_order += 1
	action for_each race_flag
		; Is this flag higher than the player's current target?
		condition if current_object.order > current_player.target_gate

		; AND less than the current candidate gate number?
		condition if current_object.order < next_order
		action set next_order = current_object.order
	end
	action set current_player.target_gate = next_order

	;tysongr - Race robustness. If the target is higher than the highest gate number, then we've completed a lap
	action for_each general
		condition if current_player.target_gate > finalCheckpoint.order

		; Lap complete, and set the target gate back to the starting gate
		action set current_player.lap_complete = 1
		action set current_player.target_gate = firstCheckpoint.order
	end

	;try to do respawning in FFA
	action for_each race_flag
		action set_respawn_filter current_object player current_player 0
		condition if current_object.order equal_to current_player.target_gate
		action navpoint_set_visible current_object player current_player 1
	end

	;tysongr - Race robustness. Partly rewritten to use previous_order defined above.
	action for_each race_flag
		;temporary number last_gate current_player.target_gate
		;action set last_gate subtract 1

		condition if current_object.order == previous_order
		action set_respawn_filter current_object player current_player 1
		action player_set_primary_respawn_object current_player current_object
		action navpoint_set_visible current_object player current_player 0
	end
end

trigger player
	action timer_set_rate current_player.lap_time -1

	condition if current_player.lap_complete == 1

	action hud_post_message player current_player none lapCompleteMessage
	action submit_incident lap_complete player current_player player none

	action set current_player.lap add 1
	;action set_score add 1 player current_player

	action for_each general
		condition if current_player.lap equal_to 1
		action set current_player.stat_best_lap_time set_to current_player.lap_time
		;action hud_post_message player current_player none "new best lap time!"
	end

	action for_each general
		condition not if current_player.lap equal_to 1
		condition if current_player.lap_time less_than current_player.stat_best_lap_time
		action set current_player.stat_best_lap_time set_to current_player.lap_time
		action hud_post_message player current_player none bestLapMessage
	end

	action timer_reset current_player.lap_time


	action for_each general
		temporary number lastlap numberOfLaps

		action set lastlap subtract 1
		condition if current_player.lap equal_to lastlap
		;action hud_post_message player current_player none finalLapMessage
		temporary number my_place_in_the_world 0
		action player_get_place current_player my_place_in_the_world
		condition if my_place_in_the_world == 1
		action submit_incident final_lap player current_player everyone
	end


	action set current_player.lap_complete = 0
end





; reset you if you're on foot for too long
trigger player
	temporary object test_for_vehicle none

	action set current_player.legal set_to 1

	action player_get_vehicle current_player test_for_vehicle

	condition if test_for_vehicle equal_to none

	action hud_widget_set_visibility widget current_player false

	action set current_player.legal set_to 0

	action timer_set_rate current_player.on_foot 1
	condition timer_expired current_player.on_foot

	; Delete his former vehicle (if the vehicle still thinks he's its rider)
	action for_each general
		temporary object vehicle current_player.vehicle
		condition if vehicle.rider == current_player or
		condition if vehicle.rider == none
		action delete_object current_player.vehicle
	end

	action timer_reset current_player.on_foot
end

; but reset timer if you get back in a vehicle
trigger player
	temporary object test_for_vehicle none
	action player_get_vehicle current_player test_for_vehicle
	condition not if test_for_vehicle equal_to none
	action timer_reset current_player.on_foot
end



trigger player
	;condition not if current_player.lap less_than numberOfLaps
	;action end_round
end

;end the round when time runs out!
trigger general
	;condition if round_time_limit > 0
	;condition timer_expired round_timer
	;action end_round
end

;delete stuff





;	# Delete Objects marked as race_delete #
	trigger race_delete
		action delete_object current_object
	end
;	# Delete Objects marked as race_delete #





;	# Delete Competitive Version Objects #
	trigger cv_delete
		condition if cvDelete == 1
			action delete_object current_object
	end
;	# Delete Competitive Version Objects #





;	# Hide Invisible Race Objects #
	trigger race_hide
		action hide_object current_object true
	end
;	# Hide Invisible Race Objects #




; vehicle cleanup

; clean up vehicles that don't seem to be in use
trigger general
	action for_each vehicle
		temporary number speedness 0
		action object_get_velocity current_object speedness
		condition if speedness less_than 50
		action timer_set_rate current_object.deathrow 1
		condition timer_expired current_object.deathrow
		action delete_object current_object
	end
end

; ...but leave the ones that are near players
trigger general
	action for_each vehicle
		temporary object test_for_player none
		temporary number distance_to_player 0
		action for_each player
			action object_get_distance current_object current_player distance_to_player
			condition if distance_to_player less_than 10
			action timer_reset current_object.deathrow
		end
	end
end


; Mark redundant, duplicate vehicles for cleaning
trigger player
	; Get the player's current vehicle
	temporary object vehicle current_player.vehicle
	condition if vehicle != none

	; Is the player's vehicle being ridden by someone else?
	action for_each general
		condition if vehicle.rider != current_player

		; How sad. Clear the rider's current vehicle so we can get him a new one
		action set current_player.vehicle = none
	end

	; Mark any vehicles who think they they are the current player's (or none's)
	action for_each vehicle
		condition if current_object != vehicle
		condition if current_object.rider == current_player or
		condition if current_object.rider == none
		action set current_object.rider = none
	end
end


; For each vehicle which should find a rider, try to find one, or delete it
trigger general
	temporary number distance 0

	; For each vehicle group, look for riderless vehicles, and find nearby riders for them (or delete them)
	action for_each vehicle
		condition if current_object.rider == none
		action for_each player
			condition if current_player.vehicle == none
			action object_get_distance current_object current_player distance
			condition if distance <= 100

			; A match! How happy! Link them up
			action set current_object.rider = current_player
			action set current_player.vehicle = current_object
		end
		condition if current_object.rider == none
		action delete_object current_object
	end
end


; After all of the above has been attempted, take care of any players who just died
trigger player
	condition player_died current_player any
	action delete_object current_player.vehicle
	action set current_player.vehicle = none
end